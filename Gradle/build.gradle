// 应用插件
apply plugin: 'java'
apply plugin: "groovy"
apply plugin: "kotlin"
apply plugin: 'maven'

sourceSets {
    main.java.srcDirs += "src/groovy"
    test.java.srcDirs += "src/groovy"
}
repositories {
    mavenCentral()
    jcenter()
    google()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
repositories {
    flatDir {
        dirs "libs"
    }
}

// 添加依赖
dependencies {
//    implementation files("../libs/Tools.jar")
    if (localBuild) {
        compileOnly project(":Tools")
        implementation "com.pqixing.gradle:tools:0.1"
    } else implementation "com.github.pqixing.modularization:Tools:$buildVersion"
    implementation gradleApi()
    implementation localGroovy()
    implementation "com.alibaba:fastjson:1.2.44"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:+'
    implementation 'org.eclipse.jgit:org.eclipse.jgit.archive:+'
}


afterEvaluate {
    project.compileGroovy.dependsOn.remove('compileJava')
    project.compileKotlin.dependsOn project.compileGroovy
    compileKotlin.classpath += files(compileGroovy.destinationDir)
    classes.dependsOn compileKotlin
}

if (!localBuild) return

// 上传到本地代码库
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: localMavenUri)
            pom.groupId = "com.pqixing.gradle" // 组名
            pom.artifactId = "modularization"// 插件名
            pom.version = "0.1" // 版本号
        }
    }
}